dependencies {
    intTestImplementation("com.nortal.test:test-automation-core:${testAutomationFrameworkVersion}")
    constraints {
        implementation('com.fasterxml.jackson.core:jackson-databind:2.14.0') {
            because 'versions below 2.14.0 have a security vulnerability'
        }
    }
    intTestImplementation("com.nortal.test:test-automation-selenide:${testAutomationFrameworkVersion}")
    intTestImplementation("com.nortal.test:test-automation-allure:${testAutomationFrameworkVersion}")
}

task e2eTest(type: Test) {
    useJUnitPlatform()

    setDescription("Runs integration tests.")
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    def e2eTestArgs = []
    if (project.hasProperty('e2eTestTags')) {
        e2eTestArgs += "-Dtest-automation.cucumber.filter.tags=" + project.getProperty('e2eTestTags')
    }
    if (project.hasProperty('e2eTestServeReport')) {
        e2eTestArgs += "-Dtest-automation.report.allure.serve-report.enabled=" + project.getProperty('e2eTestServeReport')
    }
    if (project.hasProperty('e2eTestSecurityServerUrl')) {
        e2eTestArgs += "-Dtest-automation.custom.security-server-url=" + project.getProperty('e2eTestSecurityServerUrl')
    }
    if (project.hasProperty('e2eTestCentralServerUrl')) {
        e2eTestArgs += "-Dtest-automation.custom.central-server-url=" + project.getProperty('e2eTestCentralServerUrl')
    }
    jvmArgs e2eTestArgs

    testLogging {
        showStackTraces(true)
        showExceptions(true)
        showCauses(true)
        showStandardStreams(true)
    }

    shouldRunAfter test
}
